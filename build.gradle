buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72"
    }
}

plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.8.8'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

apply plugin: 'maven-publish'

ext {
    grpc_version        = '1.27.1'
    protobuf_version    = '3.11.0'
    simulator_version   = "${simulator_version_major}.${simulator_version_minor}-SNAPSHOT"

    genDir              = file('src/gen')
    sharedDir           = file("${project.rootDir}/shared")
}

group 'com.exactpro.th2'
version "${version_major}.${version_minor}-SNAPSHOT"

repositories {
    maven {
        name 'MavenLocal'
        url sharedDir
    }
    mavenCentral()
    maven {
        name 'Artifactory snapshot'
        url 'http://artifactory5.exp.exactpro.com/artifactory/libs-snapshot'
    }
    mavenLocal()

    

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

dependencies {
    implementation "com.exactpro.th2:th2-simulator:${simulator_version}"
    implementation "javax.annotation:javax.annotation-api:1.3.2"
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.11.0"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpc_version}"
        }
    }
    generateProtoTasks.generatedFilesBaseDir = "${projectDir}/src/gen" //FIXME: Hot fix because files are generated in project from executed build
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
        ofSourceSet('main')
    }

    //Turn on if use kotlin
    //compileKotlin.dependsOn.add('generateProto')
    compileJava.dependsOn.add('generateProto')
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java', 'src/gen/main/java', 'src/gen/main/grpc'
        }
        kotlin {
            srcDirs 'src/main/kotlin'
        }
    }
}

compileKotlin {
    dependsOn "generateProto"
}

application {
    mainClassName = "com.exactpro.th2.simulator.run.SimulatorServerMain"
}

clean {
    delete genDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = 'localRepo'
            url = sharedDir
        }
    }
}


sourceCompatibility = 1.11
targetCompatibility = 1.11